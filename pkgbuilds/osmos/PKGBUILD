# Maintainer: Marek Otahal <markotahal gmail>
# Contributor: N30N <archlinux@alunamation.com>

pkgname=osmos
pkgver=1.6.1
pkgrel=2
pkgdesc="Enter the ambient world of Osmos: elegant, physics-based gameplay, dreamlike visuals, and a minimalist, electronic soundtrack."
arch=("i686" "x86_64")
url="http://www.hemispheregames.com/osmos/"
license=("custom: commercial")
depends=("openal" "mesa" "libvorbis")
_gamepkg="Osmos_${pkgver}.tar.gz"
source=("osmos.desktop")
md5sums=("7709be11fab3001b109ddf842b596d0c")
build() {
  cd $srcdir
  msg "You need a full copy of this game in order to install it"
  msg "Searching for \"${_gamepkg}\" in dir: $(readlink -f `pwd`/..)"
    pkgpath=$startdir
    if [[ ! -f "$startdir/${_gamepkg}" ]]; then
       error "Game package not found, please type absolute path to ${_gamepkg} (/home/joe):"
       read pkgpath
       if [[ ! -f "${pkgpath}/${_gamepkg}" ]]; then
           error "Unable to find game package." && return 1
       fi
    fi
  msg "Found game package, installing..."

  ln -fs ${pkgpath}/${_gamepkg} . || return 1
  tar xzf ${srcdir}/${_gamepkg} || return 1


	msg2 "Patching install path."
	sed -ri "s|^(CANONPATH=).*|\1/opt/osmos/Osmos|" Osmos/Osmos

	msg2 "Patching sound issue."
	sed -ri 's/^(soundDevice ")-/\1/' Osmos/defaults.cfg
}

package() {
	cd Osmos

	install -d "${pkgdir}/usr/bin" \
		"${pkgdir}/usr/share/applications" \
		"${pkgdir}/usr/share/licenses/${pkgname}" \
		"${pkgdir}/opt/osmos/Fonts" \
		"${pkgdir}/opt/osmos/Icons" \
		"${pkgdir}/opt/osmos/Sounds" \
		"${pkgdir}/opt/osmos/Textures"

	if [ ${CARCH} == "x86_64" ]; then
		install -m755 Osmos.bin64 "$pkgdir/opt/osmos"
	else
		install -m755 Osmos.bin32 "$pkgdir/opt/osmos"
	fi

	install -m755 Osmos "$pkgdir/usr/bin/osmos"
	install -m644 *.{cfg,loc} "${pkgdir}/opt/osmos"
	install -m644 Icons/*.png "${pkgdir}/opt/osmos/Icons"
	install -m644 Sounds/*.ogg "${pkgdir}/opt/osmos/Sounds"
	install -m644 Textures/*.tga "${pkgdir}/opt/osmos/Textures"
	install -m644 Fonts/*.ttf "${pkgdir}/opt/osmos/Fonts"
	install -m644 eula.txt \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -m644 "${srcdir}/osmos.desktop" \
		"${pkgdir}/usr/share/applications/osmos.desktop"
}

# vim: set noet ff=unix:
